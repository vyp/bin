#! /usr/bin/env nix-shell
#! nix-shell -i 'guile -e main' -p guile nano ffmpeg mpv atomicparsley youtube-dl
!#

(use-modules (ice-9 pretty-print)
             (ice-9 match)
             (srfi srfi-1)
             (alist-utils)
             (string-utils)
             (io) (pipe))

(define store         (home-path "music/store"))
(define lib           (home-path "music/lib"))
(define playlists-dir (home-path "music/playlists"))
(define status-file   (string-append store "/status.scm"))

(define (args->song args)
  (let* ((playlists (cddr args))
         (ratings (filter string->number playlists))
         (rating (if (null? ratings) #f (car ratings))))
    (->> (acons 'playlists (lset-difference equal? playlists ratings) '())
         (acons 'rating rating)
         (acons 'url (cadr args)))))

(define (current-status)
  ((lambda (port)
     (flock port LOCK_EX)
     ((lambda (status)
        (seek port 0 SEEK_SET)
        (truncate-file port)
        (cons status port))
      (read port)))
   (open-file status-file "r+")))

(define (status-add status ls song)
  (assoc-replace status ls (-> (assoc-ref status ls)
                               (append (list song)))))

(define (status-remove status ls song)
  (assoc-replace status ls (->> (assoc-ref status ls)
                                (delete song))))

(define (status-replace status ls old-song new-song)
  (-> (status-remove status ls old-song)
      (status-add ls new-song)))

(define (update-status status port)
  (pretty-print status port)
  (close-port port))

(define (get-title song)
  (assoc-replace
   song 'title (-> (string-append "youtube-dl --get-title '"
                                  (assoc-ref song 'url) "'")
                   (system-output)
                   (string-trim-right #\newline))))

(define (derive-potential-artist-and-track song)
  ((lambda (title-split)
     (->> song
          (acons 'track (if title-split (cadr title-split) #f))
          (acons 'artist (if title-split (car title-split) #f))))
   (string-substring-split (assoc-ref song 'title) " - ")))

(define (find-same-song status-list ref-song)
  (->> status-list
       (find (lambda (song) (equal? (assoc-ref song 'url)
                               (assoc-ref ref-song 'url))))))

(define (retrieve-missing-titles)
  (let* ((curstatus (current-status))
         (status (car curstatus))
         (port (cdr curstatus)))
    (->> (assoc-ref status 'wishlist)
         (filter (lambda (song) (equal? #f (assoc-ref song 'title))))
         (map get-title)
         (map derive-potential-artist-and-track)
         (fold
          (lambda (song alist)
            (status-replace
             alist 'wishlist
             (find-same-song (assoc-ref alist 'wishlist) song) song)) status)
         ((lambda (alist) (update-status alist port))))))

(define (main args)
  (mkdir-p store lib playlists-dir)
  (touch-files status-file)
  (if (> (length args) 1)
      (let* ((curstatus (current-status))
             (status (car curstatus))
             (port (cdr curstatus)))
        (-> (status-add status 'wishlist (args->song args))
            (update-status port)))
      (retrieve-missing-titles)))

; Local Variables:
; mode: scheme
; End:
