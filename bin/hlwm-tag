#!/run/current-system/sw/bin/guile \
-e main -s
!#

(use-modules (srfi srfi-1)
             (hlwm) (pipe))

(define curtag-clients (hlwm-curtag-clients))

(define (tag-next type tags)
  (find (lambda (tag) (equal? (hlwm-tag-status tag curtag-clients) type))
        tags))

(define (tag-action action tag)
  (if (equal? action "both")
      (begin (tag-action "move" tag)
             (tag-action "use" tag))
      (system* "herbstclient"
               (string-append action "_index")
               (-> (hlwm-tag-name->index tag)
                   (number->string)))))

(define (tag-urgent->occupied tag)
  (if (equal? (hlwm-tag-status tag curtag-clients) 'urgent)
      ;; ":" is the character used by herbstluftwm to signify occupied status.
      (string-append ":" (hlwm-tag-name tag))
      tag))

(define (main args)
  (let* ((tags (hlwm-tags))
         (curtag-name (hlwm-curtag-name))
         (curpos (list-index
                  (lambda (tag) (hlwm-curtag? tag curtag-name)) tags))
         (tags-before-curtag (list-head tags curpos))
         (tags-after-curtag (list-tail tags (+ 1 curpos))))
    (->> (append tags-after-curtag tags-before-curtag)
         ;; Done so that `tag-next' doesn't have to worry about processing
         ;; urgent tags.
         (map tag-urgent->occupied)
         (tag-next (string->symbol (caddr args)))
         (tag-action (cadr args)))))
