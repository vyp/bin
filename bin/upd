#! /usr/bin/env nix-shell
#! nix-shell -i 'guile -e main' -p guile git
!#
;; vim: ft=scheme

(use-modules (srfi srfi-19)
             (io-helpers)
             (pipe))

(define lockfile (home-path "dots/nixos/pkgs/versions.scm"))
(define temp-clone-dir (home-path "dl/tmp"))

(define (clone repo)
  ((lambda (clone-location date url)
     (if date
         (system* "git" "clone"
                  (string-append "--shallow-since=" date)
                  url clone-location)
         (system* "git" "clone" url clone-location))
     clone-location)
   (find-new-dir-from 1)
   (assoc-ref repo 'date)
   (assoc-ref repo 'url)))

(define (get-date rev)
  (-> (string-append "git show -s --format=%ci " rev "^")
      (system-output)
      (string-split char-whitespace?)
      (car)))

(define (date-subtract-1d date)
  ((lambda (date-string-template)
     (-> (string->date date date-string-template)
         (date->time-tai)
         (time-second)
         ;; There's 86400 seconds in a day.
         (- 86400)
         ((lambda (seconds) (make-time 'time-tai 0 seconds)))
         (time-tai->date)
         (date->string date-string-template)))
   "~Y-~m-~d"))

(define (version-info repo-location)
  (enter-dir repo-location)
  ((lambda (url tag commit)
     ((lambda (date)
        (chdir temp-clone-dir)
        (rmdir* repo-location)
        (->> (acons 'commit commit '())
             (acons 'date date)
             (acons 'tag (if (string-null? tag) #f tag))
             (acons 'url url)))
      ;; We arbitrarily subtract 1 day from the returned date just to make the
      ;; shallow clone hopefully always work regardless of timezone difference.
      (date-subtract-1d
       (get-date (if (string-null? tag) commit tag)))))
   (-> (system-output "git remote show origin -n")
       (string-split (lambda (c) (char=? c #\newline)))
       (cadr)
       (string-split char-whitespace?)
       ((lambda (ls) (filter (lambda (s) (not (string-null? s))) ls)))
       (caddr))
   (-> (system-output "git describe --abbrev=0 --tags")
       (string-trim-right #\newline))
   (-> (system-output "git rev-parse HEAD")
       (string-trim-right #\newline))))

(define (main args)
  ((lambda (port)
     ((lambda (repos)
        (enter-dir temp-clone-dir)
        (->> (map clone repos)
             (map version-info)
             (update port))
        ;; temp-clone-dir could have already existed with contents, which we do
        ;; not check for, therefore only delete it if it is empty now.
        (when (null? (fs-tree* temp-clone-dir))
              (rmdir temp-clone-dir)))
      (read port)))
   (open-file lockfile "r+")))

;; Local Variables:
;; mode: scheme
;; End:
